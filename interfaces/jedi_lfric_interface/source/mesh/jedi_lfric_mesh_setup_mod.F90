!-----------------------------------------------------------------------------
! (C) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> Provides a method to setup a  mesh for a JEDI-LFRIC
!>
module jedi_lfric_mesh_setup_mod

  use add_mesh_map_mod,        only: assign_mesh_maps
  use base_mesh_config_mod,    only: GEOMETRY_SPHERICAL, &
                                     GEOMETRY_PLANAR
  use check_configuration_mod, only: get_required_stencil_depth
  use config_mod,              only: config_type
  use constants_mod,           only: str_def, i_def, l_def, r_def
  use create_mesh_mod,         only: create_mesh
  use driver_mesh_mod,         only: init_mesh
  use extrusion_mod,           only: extrusion_type,         &
                                     uniform_extrusion_type, &
                                     TWOD
  use gungho_extrusion_mod,    only: create_extrusion
  use lfric_mpi_mod,           only: lfric_mpi_type
  use log_mod,                 only: log_event,         &
                                     log_scratch_space, &
                                     LOG_LEVEL_ERROR

  implicit none

  private

  public initialise_mesh

contains

  !> @brief Initialise the mesh and store it in the global mesh collection
  !>
  !> @param [out]   mesh_name     The name of the mesh being setup
  !> @param [in]    config        Application namelist configuration object
  !> @param [inout] mpi_obj       The mpi communicator
  !> @param [in]    alt_mesh_name The name of an alternative mesh_name to setup
  subroutine initialise_mesh( mesh_name, config, mpi_obj, alt_mesh_name )

    implicit none

    character(len=*),              intent(out) :: mesh_name
    type(config_type),              intent(in) :: config
    !> @todo: This should be intent in but when calling the method I get
    !> a compiler failure
    class(lfric_mpi_type),       intent(inout) :: mpi_obj
    character(len=*),     optional, intent(in) :: alt_mesh_name

    ! Local
    class(extrusion_type),        allocatable :: extrusion
    type(uniform_extrusion_type), allocatable :: extrusion_2d

    integer(i_def), allocatable :: tile_size(:,:)
    integer(i_def) :: tile_size_x
    integer(i_def) :: tile_size_y

    character(str_def), allocatable :: twod_names(:)
    character(str_def)              :: base_mesh_names(1)
    character(str_def)              :: prime_mesh_name
    integer(i_def),       parameter :: one_layer = 1_i_def
    integer(i_def)                  :: geometry
    integer(i_def)                  :: extrusion_method
    integer(i_def),     allocatable :: stencil_depths(:)
    integer(i_def)                  :: number_of_layers
    integer(i_def)                  :: i
    real(r_def)                     :: domain_bottom
    real(r_def)                     :: domain_height
    real(r_def)                     :: scaled_radius

    logical(l_def) :: inner_halo_tiles
    logical(l_def) :: check_partitions

    !--------------------------------------
    ! 0.0 Extract namelist variables
    !--------------------------------------
    prime_mesh_name  = config%base_mesh%prime_mesh_name()
    geometry         = config%base_mesh%geometry()
    domain_height    = config%extrusion%domain_height()
    extrusion_method = config%extrusion%method()
    number_of_layers = config%extrusion%number_of_layers()
    scaled_radius    = config%planet%scaled_radius()

    tile_size_x = 1
    tile_size_y = 1
    inner_halo_tiles = .false.

    !--------------------------------------
    ! 1.0 Create the meshes
    !--------------------------------------
    ! Choose mesh to use
    if ( present(alt_mesh_name) ) then
      !@todo: this is required until we can create multiple configurations
      base_mesh_names(1) = alt_mesh_name
    else
      base_mesh_names(1) = prime_mesh_name
    endif
    mesh_name = base_mesh_names(1)

    !--------------------------------------
    ! 1.1 Create the required extrusions
    !--------------------------------------
    select case (geometry)
    case (geometry_planar)
      domain_bottom = 0.0_r_def
    case (geometry_spherical)
      domain_bottom = scaled_radius
    case default
      call log_event("Invalid geometry for mesh initialisation", LOG_LEVEL_ERROR)
    end select

    allocate( extrusion, source=create_extrusion( extrusion_method, &
                                                  geometry,         &
                                                  number_of_layers, &
                                                  domain_height,    &
                                                  scaled_radius ) )

    extrusion_2d = uniform_extrusion_type( domain_height, &
                                           domain_bottom, &
                                           one_layer, TWOD )

    !-------------------------------------------------------------------------
    ! 1.2 Create the required meshes
    !-------------------------------------------------------------------------

    allocate(stencil_depths(size(base_mesh_names)))
    call get_required_stencil_depth( stencil_depths,  &
                                     base_mesh_names, &
                                     config )

    if (allocated(tile_size)) deallocate(tile_size)
    allocate(tile_size(2, size(base_mesh_names)))
    tile_size(1,:) = tile_size_x
    tile_size(2,:) = tile_size_y
    check_partitions = .false.

    call init_mesh( config,                      &
                    mpi_obj%get_comm_rank(),     &
                    mpi_obj%get_comm_size(),     &
                    base_mesh_names, extrusion,  &
                    inner_halo_tiles, tile_size, &
                    stencil_depths, check_partitions )

    allocate( twod_names, source=base_mesh_names )
    do i=1, size(twod_names)
      twod_names(i) = trim(twod_names(i))//'_2d'
    end do

    call create_mesh( base_mesh_names, extrusion_2d, &
                      inner_halo_tiles, tile_size,   &
                      alt_name=twod_names )
    call assign_mesh_maps(twod_names)

    deallocate(twod_names)
    deallocate(stencil_depths)
    deallocate(extrusion)
    if (allocated(extrusion_2d)) deallocate(extrusion_2d)

  end subroutine initialise_mesh

end module jedi_lfric_mesh_setup_mod
