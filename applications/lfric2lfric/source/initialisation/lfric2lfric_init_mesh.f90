!-----------------------------------------------------------------------------
! (c) Crown copyright 2024 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used
!-----------------------------------------------------------------------------
!>
!> @brief   Set up specified mesh(es) from global/local mesh input file(s).
!> @details This routine will create a mesh_object_type(s) from a
!>          specified mesh input file and extrusion.
!>
!>          The algorithm differs depending on whether the input files(s)
!>          are prepartitioned (local mesh files) or not (global mesh files).
!>
!>          The result will be:
!>            * A set of local mesh objects stored in the application local
!>              mesh collection object.
!>            * A set of mesh objects stored in the application mesh collection
!>              object.
!>
!>          Local mesh object names will use the same mesh name as given in the
!>          input file. Extruded meshes are allowed to have an alternative mesh
!>          name as several meshes could exist in memory based on the same local
!>          mesh object.
!>
module lfric2lfric_init_mesh_mod

  use add_mesh_map_mod,            only: assign_mesh_maps
  use config_mod,                  only: config_type
  use constants_mod,               only: i_def, l_def, str_max_filename
  use check_local_mesh_mod,        only: check_local_mesh
  use create_mesh_mod,             only: create_mesh
  use extrusion_mod,               only: extrusion_type
  use load_global_mesh_mod,        only: load_global_mesh
  use load_local_mesh_mod,         only: load_local_mesh
  use load_local_mesh_maps_mod,    only: load_local_mesh_maps
  use log_mod,                     only: log_event,         &
                                         log_scratch_space, &
                                         log_level_info,    &
                                         log_level_error,   &
                                         log_level_debug
  use panel_decomposition_mod,     only: panel_decomposition_type
  use partition_mod,               only: partitioner_interface
  use runtime_partition_mod,       only: mesh_cubedsphere,       &
                                         mesh_planar,            &
                                         create_local_mesh_maps, &
                                         create_local_mesh
  use runtime_partition_lfric_mod, only: get_partition_parameters
  use global_mesh_collection_mod,  only: global_mesh_collection

  ! Lfric2lfric modules
  use lfric2lfric_config_mod,      only: regrid_method_map,                   &
                                         source_geometry_spherical,           &
                                         destination_geometry_spherical,      &
                                         source_topology_fully_periodic,      &
                                         destination_topology_fully_periodic

  implicit none

  private
  public :: init_mesh

contains
!=======================================


!===============================================================================
!> @brief  Generates mesh(es) from mesh input file(s) on a given extrusion.
!>
!> @param[in] config             Application configuration object.
!>                               This configuration object should contain the
!>                               following defined namelist objects:
!>                                 * partititioning
!> @param[in] local_rank         The MPI rank of this process.
!> @param[in] total_ranks        Total number of MPI ranks in this job.
!> @param[in] mesh_names         Mesh names to load from the mesh input file(s).
!> @param[in] extrusion          Extrusion object to be applied to meshes.
!> @param[in] inner_halo_tiles   Flag to apply tiling to inner halos
!> @param[in] tile_size          Inner halo tile sizes in x/y direction for each mesh.
!> @param[in] stencil_depths_in  Required stencil depth for the application
!!                               for each mesh.
!> @param[in] regrid_method      Apply check for even partitions with the
!>                               configured partition strategy if the
!>                               regridding method is 'map'.
!>                               (unpartitioned mesh input only)
!===============================================================================
subroutine init_mesh( config,                  &
                      local_rank, total_ranks, &
                      mesh_names,              &
                      extrusion,               &
                      inner_halo_tiles,        &
                      tile_size,               &
                      stencil_depths_in,       &
                      regrid_method )

  use partitioning_nml_iterator_mod, only: partitioning_nml_iterator_type
  use partitioning_nml_mod,          only: partitioning_nml_type

  implicit none

  ! Arguments
  type(config_type),     intent(in) :: config

  integer(kind=i_def),   intent(in) :: local_rank
  integer(kind=i_def),   intent(in) :: total_ranks
  character(len=*),      intent(in) :: mesh_names(2)
  class(extrusion_type), intent(in) :: extrusion
  logical(l_def),        intent(in) :: inner_halo_tiles
  integer(i_def),        intent(in) :: tile_size(:,:)
  integer(kind=i_def),   intent(in) :: stencil_depths_in(:)
  integer(kind=i_def),   intent(in) :: regrid_method

  ! Parameters
  character(len=9), parameter :: routine_name = 'init_mesh'

  integer(kind=i_def), parameter :: dst = 1
  integer(kind=i_def), parameter :: src = 2

  ! Namelist variables
  type(partitioning_nml_type), pointer :: partitioning
  type(partitioning_nml_type), pointer :: src_partitioning_nml
  type(partitioning_nml_type), pointer :: dst_partitioning_nml

  ! partitioning namelist variables
  logical(l_def)                   :: generate_inner_halos(2)

  ! lfric2lfric namelist variables
  logical(kind=l_def)              :: prepartitioned

  character(len=str_max_filename)  :: meshfile_prefix(2)
  integer(kind=i_def)              :: geometry(2)
  integer(kind=i_def)              :: topology(2)
  integer(kind=i_def)              :: mesh_selection(2)

  ! Local variables
  integer(kind=i_def)                 :: i
  character(len=str_max_filename)     :: mesh_file(2)
  integer(kind=i_def)                 :: stencil_depths(2)

  procedure(partitioner_interface), pointer :: partitioner_src => null()
  procedure(partitioner_interface), pointer :: partitioner_dst => null()

  class(panel_decomposition_type), allocatable :: decomposition_src, &
                                                  decomposition_dst

  type(partitioning_nml_iterator_type) :: iter

  !============================================================================
  ! Extract and check configuration variables
  !============================================================================
  call iter%initialise(config%partitioning)
  do while (iter%has_next())

    partitioning => iter%next()

    if (trim(partitioning%get_profile_name()) == 'source') then
      src_partitioning_nml => partitioning
    else if (trim(partitioning%get_profile_name()) == 'destination') then
      dst_partitioning_nml => partitioning
    end if
  end do

  if (.not. associated(src_partitioning_nml)) then
    write( log_scratch_space, '(A)' )                                     &
         'Source mesh partitioning namelist (partitioning:source) not found.'
    call log_event(log_scratch_space, log_level_error)
  end if
  if (.not. associated(dst_partitioning_nml)) then
    write( log_scratch_space, '(A)' )                                          &
         'Destination mesh partitioning namelist (partitioning:destination) not found.'
    call log_event(log_scratch_space, log_level_error)
  end if

  generate_inner_halos(src) = src_partitioning_nml%generate_inner_halos()
  generate_inner_halos(dst) = dst_partitioning_nml%generate_inner_halos()

  ! Read lfric2lfric namelist
  prepartitioned       = config%lfric2lfric%prepartitioned_meshes()
  meshfile_prefix(src) = config%lfric2lfric%source_meshfile_prefix()
  meshfile_prefix(dst) = config%lfric2lfric%destination_meshfile_prefix()
  geometry(src)        = config%lfric2lfric%source_geometry()
  geometry(dst)        = config%lfric2lfric%destination_geometry()
  topology(src)        = config%lfric2lfric%source_topology()
  topology(dst)        = config%lfric2lfric%destination_topology()

  if ( regrid_method == regrid_method_map .and. &
     trim(meshfile_prefix(src)) /= trim(meshfile_prefix(dst)) ) then

    write( log_scratch_space, '(A)' )                                &
         'When using LFRic intermesh maps, source and destination '//&
         'meshes should be extracted from the same file.'
    call log_event(log_scratch_space, log_level_error)
  end if

  ! Set up stencil depths
  if ( size(stencil_depths_in) == 1 ) then
    ! Single stencil depth specified, apply to all meshes
    do i = 1, size(mesh_names)
      stencil_depths(i) = stencil_depths_in(1)
    end do
  else if ( size(stencil_depths_in) == size(mesh_names) ) then
    ! Stencil depths specified per mesh
    stencil_depths(:) = stencil_depths_in(:)
  else
    write(log_scratch_space, '(A)')                      &
        'Number of stencil depths specified does not '// &
        'match number of requested meshes.'
    call log_event(log_scratch_space, log_level_error)
  end if

  ! Check stencil depths are valid
  do i = 1, size(stencil_depths)
    if (stencil_depths(i) < 0_i_def) then
      write(log_scratch_space,'(A)') &
        'Standard partitioned meshes must support a not -ve stencil_depth'
      call log_event(log_scratch_space, LOG_LEVEL_ERROR)
    end if
  end do

  !===========================================================================
  ! Create local mesh objects:
  !  Two code pathes presented, either:
  !  1. The input files have been pre-partitioned.
  !     Meshes and are simply read from file and local mesh objects
  !     are populated.
  !  2. The input files have not been partitioned.
  !     Global meshes are loaded from file and partitioning is applied
  !     at runtime.  NOTE: This option is provided as legacy, and support
  !     is on a best endeavours basis.
  !===========================================================================
  if (prepartitioned) then

    !==========================================================================
    !  Read in local meshes / partition information / mesh maps
    !  direct from file.
    !==========================================================================
    !
    ! For this local rank, a mesh input file with a common base name
    ! of the following form should exist.
    !
    !   <input_basename>_<local_rank>_<total_ranks>.nc
    !
    ! Where 1 rank is assigned to each mesh partition.
    write(mesh_file(dst),'(A,2(I0,A))') &
        trim(meshfile_prefix(dst)) // '_', local_rank, '-', &
                                           total_ranks, '.nc'

    write(mesh_file(src),'(A,2(I0,A))') &
        trim(meshfile_prefix(src)) // '_', local_rank, '-',  &
                                           total_ranks, '.nc'

    ! Read in all local mesh data for this rank and
    ! initialise local mesh objects from them.
    !===========================================================
    ! Each partitioned mesh file will contain meshes of the
    ! same name as all other partitions.
    call log_event( 'Using pre-partitioned mesh file:', log_level_info )
    call log_event( '   '//trim(mesh_file(dst)), log_level_info )
    call log_event( "Loading local mesh(es)", log_level_info )

    if (mesh_file(dst) == mesh_file(src)) then
      call load_local_mesh( mesh_file(dst), mesh_names )
    else
      call load_local_mesh( mesh_file(dst), mesh_names(dst) )

      call log_event( 'Using pre-partitioned mesh file:', log_level_info )
      call log_event( '   '//trim(mesh_file(src)), log_level_info )
      call log_event( "Loading local mesh(es)", log_level_info )

      call load_local_mesh( mesh_file(src), mesh_names(src) )
    endif

    ! Apply configuration related checks to ensure that these
    ! meshes are suitable for the supplied application
    ! configuration.
    !===========================================================
    call check_local_mesh( config,         &
                           stencil_depths, &
                           mesh_names )

    ! Load and assign mesh maps.
    !===========================================================
    ! Mesh map identifiers are determined by the source/target
    ! mesh IDs they relate to. As a result inter-grid mesh maps
    ! need to be loaded after the relevant local meshes have
    ! been loaded.
    if (regrid_method == regrid_method_map) then
      call load_local_mesh_maps( mesh_file(dst), mesh_names )
    end if
  else

    !==========================================================================
    ! Perform runtime partitioning of global meshes.
    !==========================================================================
    if ( geometry(src) == source_geometry_spherical .and. &
         topology(src) == source_topology_fully_periodic ) then
      mesh_selection(src) = mesh_cubedsphere
      call log_event( "Setting up cubed-sphere partition mesh(es)", &
                      log_level_debug )
    else
      mesh_selection(src) = mesh_planar
      call log_event( "Setting up planar partition mesh(es)", &
                      log_level_debug )
    end if

    if ( geometry(dst) == destination_geometry_spherical .and. &
         topology(dst) == destination_topology_fully_periodic ) then
      mesh_selection(dst) = mesh_cubedsphere
      call log_event( "Setting up cubed-sphere partition mesh(es)", &
                      log_level_debug )
    else
      mesh_selection(dst) = mesh_planar
      call log_event( "Setting up planar partition mesh(es)", &
                      log_level_debug )
    end if

    call log_event( "Setting up partition mesh(es)", log_level_info )
    write(mesh_file(src),'(A)') trim(meshfile_prefix(src)) // '.nc'
    write(mesh_file(dst),'(A)') trim(meshfile_prefix(dst)) // '.nc'

    ! Set constants that will control partitioning.
    !===========================================================
    call get_partition_parameters( src_partitioning_nml, &
                                   mesh_selection(src),  &
                                   total_ranks,          &
                                   decomposition_src,    &
                                   partitioner_src )

    call get_partition_parameters( dst_partitioning_nml, &
                                   mesh_selection(dst),  &
                                   total_ranks,          &
                                   decomposition_dst,    &
                                   partitioner_dst )

    ! Read in all global meshes from input file
    !===========================================================
    if (mesh_file(dst) == mesh_file(src)) then
      call load_global_mesh( mesh_file(dst), mesh_names )
    else
      call load_global_mesh( mesh_file(dst), mesh_names(dst) )
      call load_global_mesh( mesh_file(src), mesh_names(src) )
    endif

    ! Partition the global meshes
    !===========================================================
    call create_local_mesh( mesh_names(dst:dst),           &
                            local_rank, total_ranks,       &
                            decomposition_dst,             &
                            stencil_depths,                &
                            generate_inner_halos(dst),     &
                            partitioner_dst,               &
                            enforce_constraints = .false. )

    call create_local_mesh( mesh_names(src:src),           &
                            local_rank, total_ranks,       &
                            decomposition_src,             &
                            stencil_depths,                &
                            generate_inner_halos(src),     &
                            partitioner_src,               &
                            enforce_constraints = .false. )

    ! Read in the global intergrid mesh mappings,
    ! then create the associated local mesh maps
    !===========================================================
    if (regrid_method == regrid_method_map) then
      call create_local_mesh_maps( mesh_file(dst) )
    end if

  end if  ! prepartitioned

  !============================================================================
  ! Extrude the specified meshes from local mesh objects into
  ! mesh objects on the given extrusion.
  ! Alternative names are needed in case the source and destination
  ! mesh files use the same mesh name.
  !============================================================================
  call create_mesh( mesh_names, extrusion, &
                    inner_halo_tiles, tile_size )

  !============================================================================
  ! Generate intergrid LiD-LiD maps and assign them to mesh objects.
  !============================================================================
  if (regrid_method == regrid_method_map) then
    call assign_mesh_maps(mesh_names)
  end if

end subroutine init_mesh

end module lfric2lfric_init_mesh_mod
