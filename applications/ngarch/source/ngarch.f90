!-----------------------------------------------------------------------------
! (C) Crown copyright 2024 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @page Miniapp ngarch program
!> @brief Main program used to test physics schemes for NGARCH
!> @details Runs a GungHo model with a custom step method
program ngarch

  use cli_mod,                     only : parse_command_line
  use constants_mod,               only : l_def, str_max_filename, precision_real
  use driver_collections_mod,      only : init_collections, final_collections
  use driver_comm_mod,             only : init_comm, final_comm
  use driver_config_mod,           only : init_config, final_config
  use driver_log_mod,              only : init_logger, final_logger
  use driver_modeldb_mod,          only : modeldb_type
  use driver_time_mod,             only : init_time, final_time
  use lfric_mpi_mod,               only : global_mpi
  use log_mod,                     only : log_event,       &
                                          log_level_trace, &
                                          log_scratch_space
  use timing_mod,                  only : init_timing, final_timing

  use ngarch_mod,            only : ngarch_required_namelists
  use gungho_driver_mod,     only : initialise, finalise, step
  use override_timestep_mod, only : override_timestep

  implicit none

  ! The technical and scientific state
  type( modeldb_type ) :: modeldb

  character(*), parameter   :: application_name = "ngarch"
  character(:), allocatable :: filename

  character(str_max_filename) :: timer_output_path
  logical(l_def)              :: subroutine_timers

  call parse_command_line( filename )

  call modeldb%config%initialise( application_name )
  call modeldb%values%initialise( 'values', 5 )

  ! Create the field collections in modeldb
  call modeldb%fields%add_empty_field_collection( "depository", &
                                                  table_len = 100 )
  call modeldb%fields%add_empty_field_collection( "prognostic_fields", &
                                                  table_len = 100 )
  call modeldb%fields%add_empty_field_collection( "diagnostic_fields", &
                                                  table_len = 100 )
  call modeldb%fields%add_empty_field_collection("lbc_fields",         &
                                                  table_len = 100)
  call modeldb%fields%add_empty_field_collection("radiation_fields",   &
                                                  table_len = 100)
  call modeldb%fields%add_empty_field_collection("ls_fields",          &
                                                  table_len = 100)
  call modeldb%fields%add_empty_field_collection("fd_fields",          &
                                                  table_len = 100)

  call modeldb%io_contexts%initialise(application_name, 100)

  modeldb%mpi => global_mpi

  call init_comm( application_name, modeldb )
  call init_config( filename, ngarch_required_namelists, &
                    config=modeldb%config )

  deallocate( filename )

  call init_logger( modeldb%config,         &
                    modeldb%mpi%get_comm(), &
                    application_name )

  subroutine_timers = modeldb%config%io%subroutine_timers()
  timer_output_path = modeldb%config%io%timer_output_path()

  call init_timing( modeldb%mpi%get_comm(), subroutine_timers, &
                    application_name, timer_output_path )

  call init_collections()
  call init_time( modeldb )

  call log_event( 'Initialising ' // application_name // ' ...', log_level_trace )
  call initialise( application_name, modeldb )

  call override_timestep( modeldb )

  do while ( modeldb%clock%tick() )
    call step( modeldb )
  end do

  call log_event( 'Finalising ' // application_name // ' ...', log_level_trace )
  call finalise( application_name, modeldb )
  call final_time( modeldb )
  call final_collections()
  call final_timing( application_name )
  call final_logger( application_name )
  call final_config()
  call final_comm( modeldb )

end program ngarch
