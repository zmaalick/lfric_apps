!-----------------------------------------------------------------------------
! (C) Crown copyright 2025 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief Controls the initialisation and finalisation of multifile IO
!>        for the IAU
!>
module iau_multifile_io_mod

  use base_mesh_config_mod,        only: prime_mesh_name
  use calendar_mod,                only: calendar_type
  use constants_mod,               only: str_def, str_max_filename, &
                                         i_def, r_def
  use driver_modeldb_mod,          only: modeldb_type
  use event_mod,                   only: event_action
  use event_actor_mod,             only: event_actor_type
  use field_mod,                   only: field_type
  use sci_geometric_constants_mod, only: get_chi_inventory,     &
                                         get_panel_id_inventory
  use iau_multifile_file_setup_mod,only: init_iau_inc_files
#ifdef UM_PHYSICS
  use files_config_mod,            only: iau_addinf_path, &
                                         iau_bcorr_path
  use iau_config_mod,              only: iau_ainc_multifile, &
                                         iau_use_addinf,     &
                                         iau_use_bcorr
  use iau_firstfile_io_mod,        only: iau_incs_firstfile_io

  use iau_addinf_io_nml_iterator_mod, only: iau_addinf_io_nml_iterator_type
  use iau_addinf_io_nml_mod,          only: iau_addinf_io_nml_type
  use iau_ainc_io_nml_iterator_mod,   only: iau_ainc_io_nml_iterator_type
  use iau_ainc_io_nml_mod,            only: iau_ainc_io_nml_type
  use iau_bcorr_io_nml_iterator_mod,  only: iau_bcorr_io_nml_iterator_type
  use iau_bcorr_io_nml_mod,           only: iau_bcorr_io_nml_type
#endif
  use iau_time_control_mod,        only: calc_iau_ts_num
  use inventory_by_mesh_mod,       only: inventory_by_mesh_type
  use lfric_xios_context_mod,      only: lfric_xios_context_type
  use linked_list_mod,             only: linked_list_type
  use lfric_xios_action_mod,       only: advance_read_only
  use mesh_mod,                    only: mesh_type
  use mesh_collection_mod,         only: mesh_collection
  use model_clock_mod,             only: model_clock_type
  use namelist_mod,                only: namelist_type
  use step_calendar_mod,           only: step_calendar_type

  implicit none

  private

  public :: init_multifile_io
  public :: setup_step_multifile_io
  public :: step_multifile_io
  public :: finalise_multifile_io

  private :: init_iau_incs_io
  private :: context_init

contains

  !> @brief Initialise the multifile IO
  !>
  !> @param[in]    io_context_name name of main context
  !> @param[inout] modeldb Modeldb object
  subroutine init_multifile_io(io_context_name, modeldb)

    implicit none

    character(*),          intent(in) :: io_context_name
    type(modeldb_type), intent(inout) :: modeldb

#ifdef UM_PHYSICS
    type(iau_addinf_io_nml_iterator_type) :: iter_addinf
    type(iau_ainc_io_nml_iterator_type)   :: iter_ainc
    type(iau_bcorr_io_nml_iterator_type)  :: iter_bcorr

    type(iau_addinf_io_nml_type), pointer :: iau_addinf_io_nml
    type(iau_ainc_io_nml_type),   pointer :: iau_ainc_io_nml
    type(iau_bcorr_io_nml_type),  pointer :: iau_bcorr_io_nml


    character(str_max_filename) :: filename

    character(str_def) :: name
    character(str_def) :: iau_incs
    integer(i_def)     :: iau_time

    if ( iau_use_addinf ) then
      iau_incs = 'iau_addinf_fields'
      call iau_incs_firstfile_io ( io_context_name, &
                                   modeldb,         &
                                   trim(iau_incs),  &
                                   iau_addinf_path )

      call iter_addinf%initialise(modeldb%config%iau_addinf_io)
      do while (iter_addinf%has_next())
        iau_addinf_io_nml => iter_addinf%next()
        name = iau_addinf_io_nml%name()
        filename = iau_addinf_io_nml%filename()
        iau_time = iau_addinf_io_nml%start_time()

        call init_iau_incs_io( modeldb, trim(iau_incs),  &
                               iau_time, name, filename )
      end do
    end if


    if ( iau_ainc_multifile ) then
      iau_incs = 'iau_fields'
      call iter_ainc%initialise(modeldb%config%iau_ainc_io)
      do while (iter_ainc%has_next())
        iau_ainc_io_nml => iter_ainc%next()
        name = iau_ainc_io_nml%name()
        filename = iau_ainc_io_nml%filename()
        iau_time = iau_ainc_io_nml%start_time()

        call init_iau_incs_io( modeldb, trim(iau_incs),  &
                               iau_time, name, filename )
      end do
    end if


    if ( iau_use_bcorr ) then
      iau_incs = 'iau_bcorr_fields'
      call iau_incs_firstfile_io ( io_context_name, &
                                   modeldb,         &
                                   trim(iau_incs),  &
                                   iau_bcorr_path )

      call iter_bcorr%initialise(modeldb%config%iau_bcorr_io)
      do while (iter_bcorr%has_next())
        iau_bcorr_io_nml => iter_bcorr%next()
        name = iau_bcorr_io_nml%name()
        filename = iau_bcorr_io_nml%filename()
        iau_time = iau_bcorr_io_nml%start_time()

        call init_iau_incs_io( modeldb, trim(iau_incs),  &
                               iau_time, name, filename )
      end do
    end if

#endif

  end subroutine init_multifile_io

  !> @brief Initialise the IO for the different IAU increment types
  !>
  !> @param[inout] modeldb  Modeldb object
  !> @param[in]    iau_incs Type of IAU increment
  !> @param[in]    nml_name Name of multifile namelist
  subroutine init_iau_incs_io(modeldb, iau_incs, iau_time, name, filename)
    implicit none

    type(modeldb_type), intent(inout), target :: modeldb

    character(*),       intent(in) :: iau_incs
    integer(i_def),     intent(in) :: iau_time
    character(str_def), intent(in) :: name
    character(str_max_filename), intent(in) :: filename


    type(lfric_xios_context_type), pointer :: io_context
    type(linked_list_type),        pointer :: file_list
    class( model_clock_type ),     pointer :: model_clock

    integer(i_def) :: multifile_start_timestep
    integer(i_def) :: multifile_stop_timestep

    character(str_def) :: context_name

    model_clock => modeldb%clock

    multifile_start_timestep = calc_iau_ts_num (model_clock, iau_time)
    multifile_stop_timestep  = multifile_start_timestep + 1_i_def

    context_name = "multifile_context_" // trim(name)
    call context_init(modeldb, context_name, multifile_start_timestep, &
                      multifile_stop_timestep)

    call modeldb%io_contexts%get_io_context(context_name, io_context)

    file_list => io_context%get_filelist()
    call init_iau_inc_files(file_list, modeldb, iau_incs, filename)

  end subroutine init_iau_incs_io

  !> @brief Wrapper to step the multifile IO
  !>
  !> @param[inout] modeldb            Model database object
  subroutine setup_step_multifile_io( io_context_name, modeldb )

    implicit none

    character(*),       intent(in)    :: io_context_name ! main context
    type(modeldb_type), intent(inout) :: modeldb

#ifdef UM_PHYSICS
    type(iau_addinf_io_nml_iterator_type) :: iter_addinf
    type(iau_ainc_io_nml_iterator_type)   :: iter_ainc
    type(iau_bcorr_io_nml_iterator_type)  :: iter_bcorr

    type(iau_addinf_io_nml_type), pointer :: iau_addinf_io_nml
    type(iau_ainc_io_nml_type),   pointer :: iau_ainc_io_nml
    type(iau_bcorr_io_nml_type),  pointer :: iau_bcorr_io_nml

    character(str_def) :: name

    if ( iau_use_addinf ) then
      call iter_addinf%initialise(modeldb%config%iau_addinf_io)
      do while (iter_addinf%has_next())
        iau_addinf_io_nml => iter_addinf%next()
        name = iau_addinf_io_nml%name()
        call step_multifile_io(io_context_name, modeldb, name)
      end do
    end if


    if ( iau_ainc_multifile ) then
      call iter_ainc%initialise(modeldb%config%iau_ainc_io)
      do while (iter_ainc%has_next())
        iau_ainc_io_nml => iter_ainc%next()
        name = iau_ainc_io_nml%name()
        call step_multifile_io(io_context_name, modeldb, name)
      end do
    end if


    if ( iau_use_bcorr ) then
      call iter_bcorr%initialise(modeldb%config%iau_bcorr_io)
      do while (iter_bcorr%has_next())
        iau_bcorr_io_nml => iter_bcorr%next()
        name = iau_bcorr_io_nml%name()
        call step_multifile_io(io_context_name, modeldb, name)
      end do
    end if
#endif

  end subroutine setup_step_multifile_io

  !> @brief Step the multifile IO
  !>
  !> @param[in]    io_context_name Name of main context
  !> @param[inout] modeldb         Model database object
  !> @param[in]    name            Name associated with context
  subroutine step_multifile_io( io_context_name, modeldb, name )

    implicit none

    character(*),       intent(in)    :: io_context_name ! main context name
    character(*),       intent(in)    :: name
    type(modeldb_type), intent(inout) :: modeldb

    type(inventory_by_mesh_type),  pointer :: chi_inventory
    type(inventory_by_mesh_type),  pointer :: panel_id_inventory
    type(lfric_xios_context_type), pointer :: io_context
    class(event_actor_type),       pointer :: event_actor_ptr
    type(mesh_type),               pointer :: mesh
    type(field_type),              pointer :: chi(:)
    type(field_type),              pointer :: panel_id
    class(calendar_type), allocatable      :: tmp_calendar

    character(str_def) :: context_name
    character(str_def) :: time_origin
    character(str_def) :: time_start

    integer(i_def) :: geometry
    integer(i_def) :: topology
    integer(i_def) :: coord_system
    real(r_def)    :: scaled_radius

    procedure(event_action), pointer       :: context_advance

    geometry      = modeldb%config%base_mesh%geometry()
    topology      = modeldb%config%base_mesh%topology()
    coord_system  = modeldb%config%finite_element%coord_system()
    scaled_radius = modeldb%config%planet%scaled_radius()

    chi_inventory      => get_chi_inventory()
    panel_id_inventory => get_panel_id_inventory()

    context_name = "multifile_context_" // trim(name)
    call modeldb%io_contexts%get_io_context(context_name, io_context)

    if (modeldb%clock%get_step() == io_context%get_stop_time()) then
      ! Finalise XIOS context
      call io_context%set_current()
      call io_context%set_active(.false.)
      call modeldb%clock%remove_event(context_name)
      call io_context%finalise_xios_context()

    else if (modeldb%clock%get_step() == io_context%get_start_time()) then
      ! Initialise XIOS context
      mesh => mesh_collection%get_mesh(prime_mesh_name)
      call chi_inventory%get_field_array(mesh, chi)
      call panel_id_inventory%get_field(mesh, panel_id)

      time_origin = modeldb%config%time%calendar_origin()
      time_start  = modeldb%config%time%calendar_start()

      allocate(tmp_calendar, source=step_calendar_type(time_origin, time_start))

      call io_context%initialise_xios_context( modeldb%mpi%get_comm(),      &
                                               chi, panel_id,               &
                                               modeldb%clock, tmp_calendar, &
                                               geometry, topology,          &
                                               coord_system, scaled_radius, &
                                               start_at_zero=.true. )
      call io_context%close_context_definition()

      ! Attach context advancement to the model's clock
      context_advance => advance_read_only
      event_actor_ptr => io_context
      call modeldb%clock%add_event( context_advance, event_actor_ptr )
      call io_context%set_active(.true.)
    end if

    call modeldb%io_contexts%get_io_context(io_context_name, io_context)
    call io_context%set_current()

    nullify ( chi_inventory, panel_id_inventory, mesh, chi, panel_id )

  end subroutine step_multifile_io

  !> @brief Finalise the multifile IO
  !>
  !> @param[inout] modeldb Model database object
  subroutine finalise_multifile_io(modeldb)
    implicit none

    type(modeldb_type), intent(inout) :: modeldb

  end subroutine finalise_multifile_io

  !> @brief Initialise the IO context
  !>
  !> @param[inout] modeldb Model            database object
  !> @param[in]    context_name             name of context
  !> @param[in]    multifile_start_timestep timestep to start context
  !> @param[in]    multifile_stop_timestep  timestep to stop context
  subroutine context_init(modeldb, &
                          context_name, &
                          multifile_start_timestep, &
                          multifile_stop_timestep)

    implicit none

    type(modeldb_type), intent(inout) :: modeldb
    character(*), intent(in)          :: context_name
    integer(i_def), intent(in)        :: multifile_start_timestep
    integer(i_def), intent(in)        :: multifile_stop_timestep

    type(lfric_xios_context_type)     :: tmp_io_context

    call tmp_io_context%initialise( context_name,                   &
                                    start=multifile_start_timestep, &
                                    stop=multifile_stop_timestep )
    call modeldb%io_contexts%add_context(tmp_io_context)

  end subroutine context_init

end module iau_multifile_io_mod
